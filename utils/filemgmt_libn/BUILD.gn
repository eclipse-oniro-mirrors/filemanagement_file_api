# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/filemanagement/file_api/file_api.gni")

config("libn_public_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "include/n_async",
    "${utils_path}/common/include",
  ]
}

ohos_shared_library("filemgmt_libn") {
  if (!use_mingw_win && !use_mac) {
    branch_protector_ret = "pac_ret"
    sanitize = {
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
  }

  sources = [
    "src/n_async/n_async_work_callback.cpp",
    "src/n_async/n_async_work_promise.cpp",
    "src/n_async/n_ref.cpp",
    "src/n_class.cpp",
    "src/n_error.cpp",
    "src/n_func_arg.cpp",
    "src/n_val.cpp",
  ]

  cflags_cc = [ "-std=c++17" ]
  if (use_mingw_win) {
    defines = [ "WIN_PLATFORM" ]
  }
  if (use_mac) {
    defines = [ "IOS_PLATFORM" ]
  }

  public_configs = [ ":libn_public_config" ]

  deps = [ "${utils_path}/filemgmt_libhilog:filemgmt_libhilog" ]

  external_deps = [
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  use_exceptions = true

  subsystem_name = "filemanagement"
  innerapi_tags = [ "platformsdk" ]
  part_name = "file_api"
}
